plugins {
    id "java-library"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "maven-publish"
    id "signing"
}

group = "io.github.hishidama.embulk"
version = "0.1.0"
description = "Tsurugi DB output plugin for Embulk"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

embulkPlugin {
    mainClass = "org.embulk.output.tsurugidb.TsurugiOutputPlugin"
    category = "output"
    type = "tsurugidb"
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.11"
    compile "org.embulk:embulk-util-config:0.3.3"
    compile "org.embulk:embulk-util-timestamp:0.2.2"
    compile "org.embulk:embulk-util-json:0.2.2"
    compile "org.embulk:embulk-util-retryhelper:0.8.2"

    def tsubakuroVersion = '1.0.1'
    compile "com.tsurugidb.tsubakuro:tsubakuro-connector:${tsubakuroVersion}"
    compile "com.tsurugidb.tsubakuro:tsubakuro-kvs:${tsubakuroVersion}"

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"

    testCompile "org.embulk:embulk-deps:0.11.0"
    testCompile "org.embulk:embulk-junit4:0.11.0"
    testCompile "io.github.hishidama.embulk:hm-embulk-tester:0.1.+"
}

test {
   systemProperty "endpoint", findProperty("endpoint")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'embulk-output-tsurugidb'
            from components.java

            pom {
                name = 'Tsurugi DB output plugin for Embulk'
                description = 'Inserts or updates records to a table.'
                url = 'https://github.com/hishidama/embulk-output-tsurugidb'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'hishidama'
                    }
                }
                scm {
                    connection = 'git@github.com:hishidama/embulk-output-tsurugidb.git'
                    developerConnection = 'git@github.com:hishidama/embulk-output-tsurugidb.git'
                    url = 'https://github.com/hishidama/embulk-output-tsurugidb'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username findProperty('sonatypeUsername')
                password findProperty('sonatypePassword')
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publishMavenJavaPublicationToMavenRepository") }
    sign publishing.publications.mavenJava
}

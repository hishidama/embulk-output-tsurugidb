plugins {
    id "java-library"
    id "org.embulk.embulk-plugins" version "0.7.0"
    id "com.vanniktech.maven.publish" version "0.33.0"
}

group = "io.github.hishidama.embulk"
version = "1.7.0"
description = "Tsurugi DB output plugin for Embulk"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

embulkPlugin {
    mainClass = "org.embulk.output.tsurugidb.TsurugiOutputPlugin"
    category = "output"
    type = "tsurugidb"
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.43"
    compileOnly "org.embulk:embulk-spi:0.11"
    implementation "org.embulk:embulk-util-config:0.3.3"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"
    implementation "org.embulk:embulk-util-json:0.2.2"
    implementation "org.embulk:embulk-util-retryhelper:0.8.2"

    def tsubakuroVersion = '1.12.0'
    implementation "com.tsurugidb.tsubakuro:tsubakuro-connector:${tsubakuroVersion}"
    implementation "com.tsurugidb.tsubakuro:tsubakuro-kvs:${tsubakuroVersion}"

    implementation(platform("com.fasterxml.jackson:jackson-bom:2.13.3"))
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("com.fasterxml.jackson.core:jackson-annotations")
    implementation("com.fasterxml.jackson.core:jackson-core")

    testImplementation "org.embulk:embulk-deps:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "io.github.hishidama.embulk:hm-embulk-tester:0.1.+"
    testImplementation "ch.qos.logback:logback-classic:1.4.14"
}

test {
   systemProperty "endpoint", findProperty("endpoint")
}

import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.None(), true))
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

    signAllPublications()

    pom {
        name = 'Tsurugi DB output plugin for Embulk'
        description = 'Inserts or updates records to a table.'
        url = 'https://github.com/hishidama/embulk-output-tsurugidb'
        licenses {
            license {
                name = 'MIT License'
                url = 'http://www.opensource.org/licenses/mit-license.php'
            }
        }
        developers {
            developer {
                id = 'hishidama'
            }
        }
        scm {
            connection = 'git@github.com:hishidama/embulk-output-tsurugidb.git'
            developerConnection = 'git@github.com:hishidama/embulk-output-tsurugidb.git'
            url = 'https://github.com/hishidama/embulk-output-tsurugidb'
        }
    }
}
